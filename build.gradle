plugins {
    id("org.jetbrains.kotlin.jvm") version("1.5.0")
    id("org.jetbrains.kotlin.plugin.serialization") version("1.5.0")
    id("java")
    id("com.github.johnrengelman.shadow") version("7.0.0")
    id("org.cadixdev.licenser") version("0.6.0")
}

group "me.shedaniel"
sourceCompatibility = targetCompatibility = 1.8

license {
    include "**/*.kt"
    header = project.file("HEADER.txt")
    ext {
        name = "shedaniel"
        year = "2019, 2020, 2021"
    }
}

configurations {
    shadow {
        extendsFrom(implementation)
    }
}

sourceSets {
    tricks {
        resources.srcDirs += rootProject.file("tricks")
    }
}

repositories {
    mavenCentral()
    maven { url "https://maven.fabricmc.net" }
    maven {
        url "https://jitpack.io"
        content {
            includeGroupByRegex "com(.\\w+)+"
        }
    }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    implementation("me.shedaniel:linkie-core:1.0.82") {
        exclude module: "korio"
    }
    implementation("com.discord4j:discord4j-core:3.1.3") {
        force = true
    }
//    compile("com.github.shadorc.discord4j:discord4j-core:217336e") {
//        force = true
//    }
    implementation("com.soywiz.korlibs.korio:korio-jvm:2.1.1") {
        exclude module: "kds"
        exclude module: "klock"
        exclude module: "klogger"
        exclude module: "krypto"
        exclude module: "kmem"
    }
    implementation("com.soywiz.korlibs.kds:kds-jvm:2.1.1")
    implementation("com.soywiz.korlibs.klock:klock-jvm:2.1.1")
    implementation("com.soywiz.korlibs.klogger:klogger-jvm:2.1.1")
    implementation("com.soywiz.korlibs.krypto:krypto-jvm:2.1.1")
    implementation("com.soywiz.korlibs.kmem:kmem-jvm:2.1.1")
    implementation "org.graalvm.js:js-scriptengine:20.2.0"
    implementation "org.graalvm.js:js:20.2.0"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "com.github.shedaniel:CurseMetaApi:0.2"
//        exclude(module: "truffle-api")
//    compile("org.graalvm.sdk:graal-sdk:20.2.0")
//    compile("org.graalvm.truffle:truffle-api:20.2.0")
}

task tricksZip(type: Zip) {
    from sourceSets.tricks.resources
    archiveFileName.set "global-tricks.zip"
}

jar {
    classifier "raw"
}

shadowJar {
    configurations = [project.configurations.shadow]
    classifier "shadow"
}

task mainJar(type: Jar, dependsOn: [shadowJar, tasks.tricksZip]) {
    from zipTree(shadowJar.archiveFile.get())
    from tasks.tricksZip.archiveFile
    manifest {
        attributes(
                "Main-Class": "me.shedaniel.linkie.discord.LinkieBot"
        )
    }
}

build.finalizedBy mainJar

compileKotlin {
    kotlinOptions.suppressWarnings = true
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions {
        freeCompilerArgs = ["-Xopt-in=kotlin.RequiresOptIn", "-Xinline-classes"]
        languageVersion = "1.4"
    }
}

task stage(dependsOn: ['build', 'clean'])
build.mustRunAfter clean
